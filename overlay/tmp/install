#!/usr/bin/nu

use bf
use bf-php

# Setup and install PHP
def main [] {
    # get PHP version and call installation script
    cd /tmp
    let prefix = bf env PHP_PREFIX
    let version = bf fs read PHP_BUILD
    bf write $"Installing PHP v($version)."
    let packages = [
        $"($prefix)=($version)"
        $"($prefix)-fpm=($version)"
        $"($prefix)-session=($version)"
    ]
    bf pkg install $packages

    # opcache is part of PHP >= 8.5
    if not ($version | bf version is_at_least "8.5.0") {
        bf pkg install [$"($prefix)-opcache=($version)"]
    }

    # create symlinks so PHP can be called on the commandline using php and php-fpm
    let php = "/usr/bin/php"
    let fpm = "/usr/sbin/php-fpm"
    bf write debug $"Creating symlinks to ($php) and ($fpm)."
    if ($php | bf fs is_not_symlink) { ^ln -f $"/usr/bin/(bf env PHP_PREFIX)" $php }
    if ($fpm | bf fs is_not_symlink) { ^ln -f $"/usr/sbin/(bf env PHP_PREFIX | str replace "php" "php-fpm")" $fpm }

    # save PHP version to build log
    bf build add PHP $version

    # download production php.ini file
    bf-php ini

    # make init script executable
    ^chmod +x /init

    # add bf-nginx-php module to config
    bf config use bf-nginx-php
}
